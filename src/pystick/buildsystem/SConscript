# -*- python -*-
import SCons.Errors

import cenv
from modules import add_python_module_funcs_to_env
from utils import is_building

Import('env')

AddOption('--vars', dest='vars', type='string', nargs=1, action='store', metavar='FILE', help='Variables file to use')
vars = cenv.init_global_variables(GetOption('vars'), ARGUMENTS)
env = SConscript('vars.scons', exports='env')
vars.Update(env, ARGUMENTS)
env.MergeFlags(env['XFLAGS'])


if env.get('BUILD_PATH') and env.get('PYTHON_SOURCE_PATH'):
    add_python_module_funcs_to_env(env)

    build_dir = Dir(env['BUILD_PATH']).abspath
    env.SConsignFile(Dir(build_dir).File('sconsign').path)

    env, pyconfig_defines = SConscript('configure.scons', exports='env build_dir')

    env.Append(CPPPATH=["#Include", build_dir])
    env.Repository(env['PYTHON_SOURCE_PATH'])

    # These kwargs will get passed to child scripts:
    default_sconscript_kwargs = dict(exports='env build_dir pyconfig_defines', variant_dir=build_dir, duplicate=False)

    # configure stage:
    pyconfig = env.SConscript('pyconfig.scons', **default_sconscript_kwargs)
    makefile = env.SConscript('makefile.scons', **default_sconscript_kwargs)
    configure = Alias('configure', [pyconfig, makefile])

    # build standard C modules:
    standard_python_c_modules = env.SConscript('standard_c_modules.scons', **default_sconscript_kwargs)

    # use list of external modules:
    if env.get('EXTERNAL_C_MODULES_FILE'):
        sconscript_kwargs = default_sconscript_kwargs.copy()
        sconscript_kwargs.update(variant_dir=Dir(build_dir).Dir('external_c_modules').abspath)
        external_python_c_modules = env.SConscript('external_c_modules/SConscript', **sconscript_kwargs)

    # Create Modules/config.c with all the C modules
    env.AppendPythonModulesConfiguration(standard_python_c_modules)
    env.AppendPythonModulesConfiguration(external_python_c_modules)
    config_c = SConscript('finalize_c_modules.scons', **default_sconscript_kwargs)
    default_sconscript_kwargs['exports'] += ' config_c'

    # Create a libpython that contains all the core python code.
    libpython, libpython_objects = env.SConscript('libpython.scons', **default_sconscript_kwargs)
    default_sconscript_kwargs['exports'] += ' libpython libpython_objects'

    # Create a "bare bones" Python that doesn't freeze any module, and also _sysconfigdata.py files.
    bare_python = SConscript('barepython.scons', **default_sconscript_kwargs)
    default_sconscript_kwargs['exports'] += ' bare_python'

    # Freeze Python's stdlib modules:
    freeze_modules = SConscript("freeze_stdlib.scons", **default_sconscript_kwargs)
    default_sconscript_kwargs['exports'] += ' freeze_modules'

    if env.get('EXTERNAL_PY_MODULES_FILE'):
        sconscript_kwargs = default_sconscript_kwargs.copy()
        sconscript_kwargs.update(variant_dir=Dir(build_dir).Dir('external_py_modules').abspath)
        freeze_modules = env.SConscript('external_py_modules/SConscript', **sconscript_kwargs)

    # Finalize freezing by creating the import file that declares PyImport_FrozenModules
    freeze_modules = SConscript("finalize_py_modules.scons", **default_sconscript_kwargs)

    # Pack a full Python library that can later be used by other projects
    libfullpython = SConscript('libfullpython.scons', **default_sconscript_kwargs)
    default_sconscript_kwargs['exports'] += ' libfullpython'

    # Create a full python executable for fun and games
    fullpython = SConscript('fullpython.scons', **default_sconscript_kwargs)
elif is_building():
    print("Error: to build you have to pass BUILD_PATH and PYTHON_SOURCE_PATH either in command line or a vars file (--vars)")


# TODO: help
Help("""
type:
 scons configure
""")
Help("Variables you can set either from command line or by using the --vars=file option:\n")
Help(vars.GenerateHelpText(env))